from os.path import join as pjoin


RAW_DATASETS = config["RAW_DATASETS"]
WORK = "work"
DATASETS = pjoin(WORK, "datasets")
SCALES = pjoin(WORK, "scales")


rule all:
    input:
        pjoin(DATASETS, "tkp2_exam_sentences"),
        pjoin(DATASETS, "tkp2_exam_essays"),
        pjoin(SCALES, "fivehigh.pkl"),
        pjoin(SCALES, "outof20.pkl"),


rule preprocess_tkp2:
    input:
        pjoin(RAW_DATASETS, "TKP2_tentit.xls")
    output:
        pjoin(DATASETS, "tkp2_exam.json")
    shell:
        "mkdir -p " + DATASETS +
        " && python -m finnessayscore.process_tkp {input} {output}"


rule get_tnpp_output:
    input:
        pjoin(DATASETS, "tkp2_exam.json")
    output:
        pjoin(DATASETS, "tkp2_exam.tnpp")
    singularity:
        "docker://ghcr.io/fshdnc/finnessayscore_tnpp@sha256:39ece6eef49131923553588b45ad3e61226cb2345c1af9f16e5d0fc9b8f88028"
    shell:
        "PYTHONPATH=/usr/src/app/:${{PYTHONPATH:-}} "
        "python -m preproc_tnpp.parse "
        "{input} {output}"


# XXX: Currently not working + not in use
rule get_finer_output:
    input:
        pjoin(DATASETS, "tkp2_exam.tnpp")
    output:
        pjoin(DATASETS, "tkp2_exam.finer")
    singularity:
        "docker://ghcr.io/fshdnc/finnessayscore_finer@sha256:c6232cffa903e6c89bdfbb7451c0978be11af779ada14c7a208f4d157a488436"
    shell:
        "PYTHONPATH=/usr/src/app/:${{PYTHONPATH:-}} "
        "python -m preproc_finer.parse "
        "{input} {output}"


rule mk_splits:
    input:
        pjoin(DATASETS, "tkp2_exam.tnpp")
    output:
        directory(pjoin(DATASETS, "tkp2_exam_split"))
    shell:
        "mkdir -p {output} && python -m finnessayscore.preproc.split {input} {output}"


rule clean_sentences:
    input:
        pjoin(DATASETS, "tkp2_exam_split")
    output:
        directory(pjoin(DATASETS, "tkp2_exam_sentences"))
    shell:
        "python -m finnessayscore.preproc.clean --level sentences {input} {output}"


rule clean_essays:
    input:
        pjoin(DATASETS, "tkp2_exam_split")
    output:
        directory(pjoin(DATASETS, "tkp2_exam_essays"))
    shell:
        "python -m finnessayscore.preproc.clean --level essays {input} {output}"


rule mk_grade_scale:
    output:
        pjoin(SCALES, "{scale}.pkl"),
    shell:
        "python -m finnessayscore.mk_grade_pickle {wildcards.scale} {output}"
